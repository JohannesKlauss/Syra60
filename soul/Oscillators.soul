graph Oscillators
{
    input midiParser.parseMIDI midiIn;

    input event float
        detuneFactorParam, sawLevelParam, subLevelParam, pulseLevelParam, pulseWidthModulationParam, vcaLevelParam, noiseLevelParam, hpfFrequencyParam,
        chorusModeParam, lfoRateParam, lfoDelayParam, lfoPitchModulationParam, lfoTriggerModeParam, pulseWidthModModeParam,
        vcfCutoffParam, vcfResoParam, vcfEnvPolarityParam, vcfEnvModDepthParam, vcfLfoModDepthParam, vcfKeyModDepthParam,
        envAttack, envDecay, envSustain, envRelease;

    output stream float<2> audioOut;

    let
    {
        midiParser = soul::midi::MPEParser;

        lfo = Syra60::LFO;
        vca = Syra60::VCA;
        lfoEnv = Syra60::DAREnvelope;
        chorus = Syra60::Chorus(7237.0f, 10644.0f);
        hpf = Syra60::SinglePoleFilter(true, 0.0f);
        controller = Syra60::Controller;
        softClip = SoftClip;

        voices = Voice[6];
        voiceAllocator = soul::voice_allocators::Basic(6);

        hpfFrequency = ParameterStream;
        vcaLevel = ParameterStream;
    }

    connection
    {
        hpfFrequencyParam -> hpfFrequency.parameterUpdate;
        vcaLevelParam -> vcaLevel.parameterUpdate;

        lfoRateParam -> lfo.rate;
        lfoDelayParam -> lfoEnv.delay;

        // Forward events
        detuneFactorParam -> voices.detuneFactorParam;
        sawLevelParam -> voices.sawLevelParam;
        subLevelParam -> voices.subLevelParam;
        pulseLevelParam -> voices.pulseLevelParam;
        pulseWidthModulationParam -> voices.pulseWidthModulationParam;
        pulseWidthModModeParam -> voices.pulseWidthModModeParam;
        noiseLevelParam -> voices.noiseLevelParam;
        lfoPitchModulationParam -> voices.lfoPitchModulationParam;
        vcfCutoffParam -> voices.vcfCutoffParam;
        vcfResoParam -> voices.vcfResoParam;
        vcfEnvPolarityParam -> voices.vcfEnvPolarityParam;
        vcfEnvModDepthParam -> voices.vcfEnvModDepthParam;
        vcfLfoModDepthParam -> voices.vcfLfoModDepthParam;
        vcfKeyModDepthParam -> voices.vcfKeyModDepthParam;

        chorusModeParam -> chorus.chorusMode;

        lfo.audioOut -> voices.lfoValue;
        vcaLevel.audioOut -> vca.levelIn;
        hpfFrequency.audioOut -> hpf.cutoffIn;

        envAttack -> voices.envAttack;
        envDecay -> voices.envDecay;
        envSustain -> voices.envSustain;
        envRelease -> voices.envRelease;

        // Pass the events to the voice allocator
        midiParser.eventOut -> voiceAllocator.eventIn;
        midiParser.eventOut -> controller.eventIn;

        controller.note1_active -> lfoEnv.triggerLevel;

        lfoEnv.envelopeLevel -> lfo.envelopeLevelIn;
        lfoTriggerModeParam -> lfo.triggerMode;

        // Pass the allocated events to the voice bank
        voiceAllocator.voiceEventOut -> voices.eventIn;

        voices -> hpf.audioIn;
        hpf.audioOut -> softClip -> vca.audioIn;
        vca.audioOut -> chorus.audioIn;

        chorus.audioOut -> audioOut;
    }
}
